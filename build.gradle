plugins {
    id "java"
    id "maven"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'net.minecrell.licenser' version '0.4.1'
}

group = 'org.enginehub'
version = '1.0-SNAPSHOT'

defaultTasks 'licenseFormat', 'build'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(12))

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://m2.dv8tion.net/releases'
    }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        url = 'https://jitpack.io'
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.google.guava:guava:18.0'
    }
}

license {
    newLine = false
    header = project.file('HEADER.txt')
    include '**/*.java'
}

tasks.compileJava {
    options.compilerArgs = ["-parameters"]
}

task sourceJar(type: Jar) {
    classifier = 'source'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    classifier = 'dev'
}

shadowJar {
    classifier = ''
}

dependencies {
    implementation 'net.dv8tion:JDA:4.3.0_277'
    implementation(group: 'org.spongepowered', name: 'configurate-hocon', version: '3.7.2')
    implementation 'com.typesafe:config:1.4.0'
    implementation 'com.sk89q:intake:3.1.2'
    implementation platform("com.fasterxml.jackson:jackson-bom:2.11.2")
    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names"
    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

artifacts {
    archives shadowJar
    archives sourceJar
    archives javadocJar
    archives jar
}

build.dependsOn(shadowJar)
